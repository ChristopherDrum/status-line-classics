Object Crypt "Crypt"
  with  s_to bronze_mural,
        u_to Patio_in_Maze,
        d_to bronze_mural,
        initial [; MazeX = 7; MazeY = 5; rfalse; ],
        description "A dismal crypt, disused, rainswept and strewn with leaves. Light streams in from the square hole in the roof. There are no bones or urns on show.",
        section T_CATACOMBS,
  has   light;

LagachObj -> bronze_mural "bronze mural"
  with  name 'bronze' 'mural',
        door_to Jagged_Passage,
        when_closed "The south wall forms a giant bronze mural, which has stood the test of time.",
        door_dir d_to,
        when_open "The mural stands slightly to one side, revealing a passage leading downward.",
        description "The mural depicts an old bearded wise man following a star in the western sky. He has his right arm around an attractive young woman and holds a bundle of wands with his left hand. Around the border are astrological symbols of all kinds, from Tarot suits to zodiacal constellations.",
  has   door static;

[ CryptRoomBefore i ;   
    if( action == ##Smell ) {
        if( location ~= The_Catacombs ) "Ugh.";

        i = The_Catacombs.time_left + 1;
        The_Catacombs.time_left = i;
        if( i == 1 ) { 
            "Something like... perhaps, air after a thunderstorm... but no, you still can't be sure what it is.";
        }
        if( i == 2 ) {
            move aluminium_staircase to The_Catacombs;
            The_Catacombs.d_to = Universe_Maintenance_Room;
            OzoneSmelled = true;
            Achieved(SCORE_SMELL);
            "Ozone!  That's what it's called. Funny sort of odour for a cave - especially since, on investigating further, you find that it's drifting up from a concealed aluminium staircase.";
        }
        "Definitely ozone.";
    }
    rfalse;
];

Object Jagged_Passage "Jagged Passage"
  with  name 'bones',
        before CryptRoomBefore,
        n_to Crypt,
        s_to The_Catacombs,
        e_to Charnel_Passage,
        u_to Crypt,
        d_to The_Catacombs,
        description "An eerie passage, running down from an opening at the north to murky depths in the south. The walls are jagged, uneven and decorated with bones. An unpleasantly stale, charnel odour drifts in through a gap to the east.",
        section T_CATACOMBS,
  has   ;

Object -> wrought_iron_key "long wrought iron key"
  with  name 'long' 'wrought' 'iron' 'key',
        sw_to compass,
        initial "You appear to be standing on a wrought iron key.",
        description "A figure-eight double ring, a long barrel and a finely-cut ten-groove claw: this is the ultimate in keys. It is superb.",
  has   ;

Object Charnel_Passage "Charnel Passage"
  with  before CryptRoomBefore,
        e_to Sarcophagus,
        w_to Jagged_Passage,
        description "A long, winding, vile passage through the earth, running east to west. The distasteful odour grows stronger as you go east.",
        section T_CATACOMBS,
  has   ;

Object Sarcophagus "Sarcophagus"
  with  before CryptRoomBefore,
        w_to Charnel_Passage,
        description "Well, perhaps ~sarcophagus~ is a little melodramatic, but it's that kind of moment. This must be somewhere under the old parish church. You are stooped over inside a stone tomb, stained and crumbled with decay, broken only by a crevice you crawled in by. It is a nightmarish place.",
        section T_CATACOMBS,
  has   ;

Object -> tombstone "tombstone"
  with  name 'tombstone' 'tomb' 'stone',
        initial "Nightmarishly, even the floor you kneel on is a fallen tombstone.",
        description "The gist of the inscription is: ~Henri Maladreue, obiit mcdlvi.~",
  has   static;

Object The_Catacombs "Catacombs"
  with  before CryptRoomBefore,
        n_to Jagged_Passage,
        w_to CR,
        u_to Jagged_Passage,
        d_to nothing,
        description "A large, grisly cave, deep under the garden, dimly lit and eerie. Darkness curls around the natural pillars of rock like a mist: and there is a peculiar, vaguely familiar odour to it. The only obvious routes out are an uneven passage climbing to the north and a crawl west.",
        cant_go "You bumped into something unpleasant there.",
        time_left 0,
        section T_CATACOMBS,
  has   ;

Object aluminium_staircase "aluminium staircase"
  with  name 'staircase' 'stairs' 'stair' 'case' 'aluminium',
        before [; Climb, Enter: << Go d_obj >>; ],
        initial "The odd smell seems to be ozone, and it drifts up from a previously concealed aluminium staircase.",
        description "Leading down.",
        article "an",
  has   static;

[ AdjustSlide i j u v ; 
    u = henri_post_array->i;
    v = henri_post_array->j;
    henri_post_array->j = u;
    henri_post_array->i = v;
    rtrue;
];

[ DisplaySlidePos ndx chr ;      
    chr = henri_post_array->(ndx - 1);
    if( chr == 0 ) {
        print ". ";
        rfalse;
    }
    chr = chr + 65 - 1; 
    print (char) chr, " ";
    rtrue;
];

[ DisplayWholeGrid ;  
    0-->8 = 2 | 0-->8;
    print "     ";
    DisplaySlidePos(1);
    DisplaySlidePos(2);
    DisplaySlidePos(3);
    print "^      + +^", "     ";
    DisplaySlidePos(4);
    DisplaySlidePos(5);
    DisplaySlidePos(6);
    print "^      + +^", "     ";
    DisplaySlidePos(7);
    DisplaySlidePos(8);
    DisplaySlidePos(9);
    print "^      + +^", "     ";
    DisplaySlidePos(10);
    DisplaySlidePos(11);
    DisplaySlidePos(12); 
    print "^      + +^", "     ";
    DisplaySlidePos(13);
    DisplaySlidePos(14);
    DisplaySlidePos(15);
    new_line;
    0-->8 = 65533 & 0-->8;
    rtrue;
];

Object CR "CR"
  with  name 'basket' 'seesaw' 'hand' 'cannon' 'glass',
        after [;
            Go, Enter:
                if (noun == n_obj or s_obj or metal_wall) {
                    print "^Walking through the mirror is a wrench, but leaves you somewhere... almost exactly 
                        the same...^";
                    redraw_statusline = true;
                    Achieved(SCORE_REFLECTING);
                    if (noun == n_obj) {
                        self.s_to = metal_wall;
                        self.n_to = nothing;
                        metal_wall.door_dir = s_to,
                        self.time_left = 3;
                    } else {
                        self.n_to = metal_wall;
                        self.s_to = nothing;
                        metal_wall.door_to = n_to,
                        self.time_left = 2;
                    }
                    AdjustSlide(0,2);
                    AdjustSlide(3,5);
                    AdjustSlide(6,8);
                    AdjustSlide(9,11);
                    AdjustSlide(12,14);
                }
                rfalse;

        ],
        n_to metal_wall,
        s_to nothing,
        e_to [;
            if( self.time_left == 3 )
                "You bump into an invisible barrier - perhaps the edge of the reflection.";
            return The_Catacombs;
        ],
        description "The cartoonist Heath Robinson used to specialise in drawings of fantastically complicated 
            machines of string, pulleys, levers, counterweights, cogs, mice running about on wheels and the
            like. Just such a contraption fills the west end of this room, though most of the workings are behind
            a glass wall which, although running with age, is as solid as the day it was built. The parts are very
            old (hand-made, not machined) but uncorroded.^^The only way out appears to be via the crawl to the east.",
        time_left 2,
        short_name [;
            if( self.time_left == 3 ) 
                print "Contraption Reflection";
            else
                print "Contraption Room";
            rtrue;
        ],
        section T_CATACOMBS,
  has   ;

Object -> metal_wall "metal wall"
  with  name 'mirror' 'metal' 'bronze' 'wall' 'sheet' 'amber',
        before [;
            Rub: "You polish the metal a little, but there's far too much to do by hand.";
        ],
        door_to CR,
        when_closed "The north edge of the room is a big sheet of dull amber-coloured metal.",
        door_dir n_to,
        when_open [;
            print "The whole "; 
            if( CR.time_left == 2 ) {
                print "north";
            } else {
                print "south";
            } 
            " wall is occupied by a bronze mirror, reflecting the room.";
        ],
        description "Someone rather good-looking (you can't help thinking) is reflected in the metal surface.",
  has   door static;

Object -> iron_grid "iron grid"
  with  name 'iron' 'grid' 'contraption' 'panel',
        initial [;
            print "Amid the glass wall is an iron panel divided into a three-by-five grid.^^Behind the glass is a kind of see-saw of two leather baskets at opposite ends of an oak pivot. ", "In one corner of the roof, above the far end of the see-saw, is a ";
            if( ancient_well notin nothing ) {
                print "hole through which dim natural light emanates.^^";
            } else {
                print "dark hole.^^";
            }
            print "Projecting out of the back of the panel mechanism at the near end is a metal hand, which ";
            if( CR has general ) {
                "grasps empty air.";
            }
            "grasps a cannonball in mid-air.";
        ],
        description [;
            print "There are fourteen pieces in the grid, and one gap. The pieces have letters upon them ";
            if( CR.time_left == 3 ) {
                print "(in reflected script) ";
            }
            print "and are arranged as follows:^^";
            DisplayWholeGrid();
            "^[Try ~slide b~, for instance, to move a piece into the gap.]";
        ],
  has   static;

Object -> glowing_golden_orb "glowing golden orb"
  with  name 'glowing' 'golden' 'orb',
        before [; default: "It is out of reach, beyond the glass."; ],
        description "How tantalisingly valuable it seems.",
  has   scenery;

Object -> cannonball "cannonball"
  with  name 'cannon' 'ball' 'cannonball',
        before [;
            Examine: "It is out of reach, beyond the glass.";
        ],
        description "Solid, heavy, reliable, out of reach on the other side of the glass.",
  has   scenery;

Object Universe_Maintenance_Room "Universe Maintenance Room"
  with  name 'gauges' 'machines' 'cube' 'dials',
        u_to The_Catacombs,
        description "You really should try to be more awestruck, for this is Merlin's cave, the hub of Creation. Every visitor perceives this place differently, according to his or her own myths and beliefs. Your particular creed being science, there is only a black metal one-metre cube with an unimpressive collection of gauges and dials. An aluminium staircase leads up.",
        section T_CATACOMBS,
  has   scored;

Object -> big_electricians_switch "big electrician's switch"
  with  name 'big' 'electricians' 'switch',
        after [;
            SwitchOn: "Click-clunk!  The world looks precisely the same.";
            SwitchOff: "Clunk-click!  Probably nothing very much has changed.";
        ],
        description "The lower, on, position is labelled ~Determinism~. The upper, off, position is labelled ~Chance~.",
  has   on scenery static switchable;

Object -> large_dial "large dial"
  with  name 'large' 'dial' 'gauges' 'dials',
        before [;
            Turn:
                if( PlanckWarning == false ) {
                    PlanckWarning = true;
                    trigger_quotation(12);
                    "Is it really wise to go meddling with things here?  Well, only one way to find out. Don't expect a second warning!";
                }
                deadflag = DESTROYED_UNIVERSE;
                score = 65437;
                "Mmm. On the whole, it's probably best not to alter one of the fundamental constants of the physical universe while still inside it. On the bright side, for once you are unlikely to get the blame.";
        ],
        initial "Particularly prominent are a large dial, like the volume control on an expensive hi-fi amplifier, and a big electrician's switch. Both are labelled in small print.",
        description "Above the dial is a lower-case letter h with a slashed line through it. It is currently turned to 1.055 or thereabouts. There is something worryingly dangerous about its matter-of-factness.",
  has   static;

Object Inside_the_Orb "Inside the Orb"
  with  name 'light',
        after [;
            Strike: "The sphere rings with a hollow noise, but the rod charges up.";
        ],
        description "You half-stand, half-float in a golden, misty sphere perhaps ten yards across, which slowly spins. The centre, out of reach and somehow insubstantial, is an eight-by-eight lattice of oblong crystals, bathed in warm pearly light.",
        number 0,
        each_turn [ i j;
            i = Inside_the_Orb.number + 1;
            Inside_the_Orb.number = i;
            if( i == 1 ) {
                rtrue;
            }
            if( i == 2 ) {
                print "^The sphere rotates, giving you a fresh view of the images: stylised Kings and Queens?^";
            }
            if( i == 4 ) {
                print "^The sphere rotates, giving you another look: humble tokens of warriors.^";
            }
            if( i == 6 ) {
                print "^The sphere rotates, giving you another look: carved horse-heads and Bishops' mitres.^";
            }
            if( i == 8 ) {
                print "^(The sphere rotates.)  It's a chess game!  Now you understand. But you still can't puzzle out the actual solution.^";
            }
            if( i == 10 ) {
                print "^(The sphere rotates.)  You have some impression that White is trying to make an attack, having plenty of spare pieces.^";
                trigger_quotation(26);
            }
            if( i == 12 ) {
                print "^(The sphere rotates.)  Seeing the board from Black's point of view, the defence looks solid.^";
            }
            if( i == 14 ) {
                print "^(The sphere rotates.)  Back to White's side. You definitely need to smash through somehow, all that cautious exchanging of pieces hasn't worked. If you want White to win, that is.^";
            }
            j = i % 4;
            if( j == 0 ) {
                give Inside_the_Orb ~general;
                if( i > 12 ) {
                    "^(The sphere rotates.)  Black still looks safe.";
                }
            }
            if( j == 2 ) {
                give Inside_the_Orb general;
                if( i > 14 ) {
                    "^(The sphere rotates.)  White must break out somehow.";
                }
            }
        ],
        section T_CATACOMBS,
  has   light;

Object -> array_of_oblong_crystals "array of oblong crystals"
  with  name 'array' 'oblong' 'lattice' 'crystals' 'images' 'board' 'chess' 'pieces' 'king' 'queen' 'white' 'black' 'kings' 'queens' 'bishops' 'knights',
        initial "Images lurk about the array of oblong crystals.",
        description "They are extremely hard to see and impossible to touch, as if you can only catch anything at all through the corner of your eye.",
        article "an",
  has   static;


