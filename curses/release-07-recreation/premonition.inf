Class   PremoR
  with  before [;
            Go, Take, Look, Inv, Wake: rfalse;
            default: "You seem unable to concentrate sufficiently.^";
        ],
  has   light;

PremoR  Premonition "Premonition"
  with  description "It is a frosty, clear night, but there is a scent of 
            camp-fires burning in the distance. You are passing through
            the landscape as if a ghost, and all seems faintly unreal. To
            the east is one side of an animal-hide tent, but there is
            no way in from here. To southwest, some soldiers sit around
            the embers of a fire. There is a terrible sense of something
            about to happen.",
        e_to [;
            print "You pass uncomfortably straight through the hide wall.^";
            return The_Generals_Tent;
        ],
        sw_to Camp_Fire,
        cant_go "You drift in that direction, but without gaining any ground.",
  has   light;

PremoR  The_Generals_Tent "The General's Tent"
  with  description "This is the tent of a thin, reedy man who wears no 
            uniform but has an obvious and commanding presence. He has an
            aquiline, patrician nose and, at five feet four, is taller
            than anyone else present. The others are advisers, junior
            officers, scheming politicians and their like. A strange,
            more solid figure stands by a trestle table at the north
            end of the tent.",
        n_to Beside_Table,
        w_to [;
            print "You pass uncomfortably straight through the hide wall.^";
            return Premonition;
        ];

PremoR  Beside_Table "Beside Table"
  with  description "Beside the table is a more substantial adviser, as if 
            she too walks in this strange spirit plane. She is wearing
            frightening druidical robes, of strange pelts and furs,
            with nasty-looking charms around her neck. Every so often,
            she seems to catch sight of you out of the corner of her
            eye, but then look round and see nothing. The military
            men do not believe in her power, and yet... they treat her
            with respect.",
        s_to The_Generals_Tent;

Object  -> heavy_iron_mascot "heavy iron mascot"
  with  name 'heavy' 'iron' 'mascot',
        description "The mascot is in the shape of an ankh, with a lemniscus 
            (or infinity) symbol moulded onto it.",
        initial "On the table is a heavy iron mascot of some kind.",
        number 15,
        before [;
            Give:
                "Now that really would not be very kind.";
        ],
        daemon [;
            if( self notin player && ~~IndirectlyContains(player, self) )
                rfalse;
            self.number--;
            switch( self.number ) {
                0: deadflag = DIED;
                   "^In an astonishing freak accident, a meteorite hurtles through the Earth's atmosphere and then straight through your head. Anyone would think you had a curse on you (anyone, that is, still able to think).";
                2: "^Something feels very wrong indeed. Your hand begins to burn.^";
                5: "^You scratch yourself, unable to get rid of an itching sensation.^";
                10: print "^You feel very nervous indeed, for some reason.^";
            }
        ],
        devil_hint "An absolute essential.",
  has   ;

PremoR  Camp_Fire "Camp Fire"
  with  description "You can almost smell fear here. A motley platoon of 
            soldiers are sitting about the embers of a fire. None are
            talking or sleeping, and the false good humour of the evening
            has died away. At first light, you realise, there will be a
            battle. It isn't a risk you'd like to run in their place. And
            there is a strange light already in the east... not yet a
            dawn, for the soldiers seem unaware of it.",
        e_to Near_Dolmen,
        ne_to Premonition;

PremoR  Near_Dolmen "Near Dolmen"
  with  description "A standing stone, perhaps a glacial erratic, stands 
            here and on top of it is a shining blue stone, whose
            brightness almost blinds you. You feel irresistably drawn
            to it, and your hand reaches out...",
        name 'bluish' 'stone' 'dolmen' 'brightness',
        each_turn [;
            game_section = MODERN;
            while( child(temp_location) )
                move child(temp_location) to player;
            if( parent(heavy_iron_mascot) == player ) {
                StartDaemon(heavy_iron_mascot);
                Achieved(SCORE_DOLEMN);
                score = score - 50;
            }
            PlayerTo(Servants_Room, 1);
            move player to servants_bed;
            "^You awaken, shivering with dread.";
        ];

