Object Museum_of_Arcana "Museum of Arcana"
  with  name 'shadow',
        description "A dark hall, deserted after nightfall. Outside it is a 
            stormy October night, and rain beats against the
            windowpanes. Shadows swoop and dive in the air like bats.",
        cant_go "There's no apparent way out of here.",
        time_left 0,
        time_out [;
            print "^Doktor Stein appears and steps toward you, clapping his 
                hands sharply. Suddenly dozens of robed figures leap out
                of the darkness. Their faces are painted hideously and
                they betray no emotion as they pinion you tight...^^Stein
                forces your mouth open and makes you swallow a violet
                pill. ~One of your apothecary ancestors made this drug!
                Appropriate, no?~^^", "You feel queasy, but this is
                perhaps the least of your problems as they drag you
                off...^^";
            give player general;
            PlayerTo(Coven_Cell, 2);
            StartTimer(Coven_Cell, 5);
        ],
  has   scored;

Object  -> Tarot_box "Tarot box"
  with  name 'box' 'of' 'tarot',
        devil_hint "There's nothing worthwhile in it.",
        before [;
            Receive:
                if( noun hasnt card )
                    "Only cards will fit.";
                rfalse;
        ],
  has   container openable;

Tarot   -> -> Castle_card "Castle"
  with  name 'castle' 'card',
        description "A stone castle on a hill.",
        article "the",
        door_to Ruined_Castle_Cafe,
        devil_hint "It leads nowhere.";

Tarot   -> -> Star_card "Star"
  with  name 'star' 'card',
        description "A single bright light in the darkness.",
        article "the",
        door_to Lighthouse,
        devil_hint "Ugh, what a nasty bright light. Good thing you'll never go there.";

Tarot   -> -> Eight_of_Wands_card "Eight of Wands"
  with  name 'eight' 'of' 'wands' 'card',
        article "the",
        rod_assoc rod_of_infinity,
        devil_hint "There's less to this card than meets the eye.",
  has   hidden_rod;

Tarot   -> -> Maiden_card "Maiden"
  with  name 'maiden' 'card',
        description "Andromeda chained to a rock on the sea-shore.",
        article "the",
        door_to Sea_Shore,
        devil_hint "What an ugly girl!  Bet she's good for a laugh though.";

Rod     rod_of_infinity "Rod of Infinity"
  with  name 'rod' 'of' 'infinity' 'card',
        rod_action [;
            if( second == player )
                "You acquire no especial sense of the infinite.";
            if( second ~= lemniscus )
                "Nothing obvious happens.";
            if( circular_arc.number < 6 )
                "You feel the air charge with excitement, and the symbol 
                 glows fiery red for a moment, but then the power drains
                 away into one of the empty sockets in the arc.";
            if( self has general )
                "Nothing happens this time.";
            print "The air charges with excitement, and the symbol glows fiery red, then 
                begins to spin, faster and faster until everything
                becomes a daze and you find yourself...^";
            give self general;
            TriggerEndgame();
        ],
        angel_hint "Well, where might you have seen infinity before?";

[ TriggerEndgame i j;
    game_section = ANCIENT_BRITAIN;
    for( i = child(player) : i : i = j ) {
        j = sibling(i);
        give i ~worn;
        move i to Infinity_Symbol;
    }
    PlayerTo(Murky_Cave, 2);
    move historical_pamphlet to player;
    rtrue;
];

Tarot   Ace_of_Cups_card "Ace of Cups"
  with  name 'ace' 'of' 'cups' 'card',
        description "It shows a great many crystal-glass cups and glasses.",
        article "the",
        door_to Cups_and_Glasses,
        devil_hint "It leads nowhere.";

Object  Coven_Cell "Coven Cell"
  with  description "An old administrative office of the museum, emptied and 
            made into a prison cell: amateur but effective.",
        before [;
            if( player has general && self.time_left < 5 )
            "You seem to lack the will any more. The drug must have taken hold.";
        ],    
        time_left 0,
        time_out [;
            if( player notin self )
                rfalse;
            print "^A key turns in the door and Doktor Stein bursts in. 
                He stops to pat the goat affectionately. ~Cheer up,
                Dobbin. As soon as I've finished this brutal human
                sacrifice I'll take you for your walk.~  He feeds
                Dobbin some sugar-cake. ~They make such marvellous
                cheese,~ he explains confidentially.^";
            deadflag = DIED;
            if( player has general )
                "^In a trance, you go willingly to your (somewhat messy) sacrifice...";
        ],
        cant_go "There's definitely no way out of here.",
  has   ;

Object  -> goat "anxious-looking goat"
  with  name 'anxious' 'goat',
        article "an",
  has   static;

Object  -> crook "shepherd's crook"
  with  name 'shepherd' 'crook',
        rod_assoc rod_of_husbandry,
        devil_hint "Just a crook. And what's wrong with crooks, that's what I say.",
  has   hidden_rod;

Object  -> cell_door "cell door"
  with  name 'cell' 'door',
        initial "The door to the south stands open.",
        door_dir s_to,
        door_to Museum_of_Arcana,
        describe "As you might expect, the cell door is closed.",
  has   door openable static locked;

Rod     rod_of_husbandry "Rod of Husbandry"
  with  name 'rod' 'of' 'husbandry',
        article "the",
        rod_action [;
            if( second == player )
                "You feel suddenly obedient, and briefly fancy a dog biscuit.";
            if( second ~= herd_of_goats )
                "Nothing obvious happens.";
            give herd_of_goats general;
            "The goats suddenly look up at you with awe, although they resume 
             their former interest in eating the grass quite quickly.";
        ],
        angel_hint "Perhaps you should look up ~husbandry~ in a dictionary.";
