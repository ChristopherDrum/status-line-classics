 ! Global CouncilCnt;
 ! recoded using kitchen_well.number, plus a daemon

Object  Murky_Cave "Murky Cave"
  with  description "This is a murky hillside cave, whose mouth opens to the 
            northeast, though a tight squeeze might lead a little way
            west. Outside is a clear starry night. There is no traffic
            noise, and the air smells fresh.",
        w_to Spur_of_Rock,
        ne_to Valley_Prospect,
  has   ;

Object  -> flaming_torch "flaming torch"
  with  name 'flaming' 'torch',
        initial "Hanging up on an iron bracket is a flaming torch.",
  has   light;

Object  -> historical_pamphlet "historical pamphlet"
  with  name 'history' 'pamphlet',
        description "It is a summary of the writings of a polemical monk 
            called Gildas, and relates to events following the fall of the
            tyrant Vortigern at the turn of the fifth century A.D. After
            dark decades of pillage by Saxon mercenaries, Britain was
            reunited by Ambrosius Aurelianus and then defended at the
            Battle of Badon Hill, some time in the early years of the
            sixth century.^^This remarkable victory was of no religious
            significance, so Gildas does not dwell upon it. He tends only
            to mention bad kings and leaders, and then only to insult
            them. (Talking about Gildas is an excellent way to annoy
            an Anglo-Saxon historian.)  Nobody even knows where Badon
            Hill is any more, or anything else about it, although some
            students of Welsh poetry believe Ambrosius was the source
            of the legend of King Arthur.",
        article "an",
  has   ;

Object  Spur_of_Rock "Spur of Rock"
  with  description "An uncomfortable spur of rock on the hillside crags. 
            The only natural access to this spot is a crevice in the
            hill to the east, but there is also a securely fastened rope
            hanging down into the darkness.",
        e_to Murky_Cave,
        d_to Hanging_on_Rope,
        cant_go "The crags are too precipitous.",
  has   light;

Object  -> hanging_rope "hanging rope"
  with  name 'rope',
  has   scenery;

Object  Hanging_on_Rope "Hanging on Rope"
  with  name 'patrol' 'men',
        description "You are hanging perilously on a hemp rope, suspended from an 
            overhang of rock above, which drops down into the darkness of
            the valley. On the tracks below, patrols of men can be heard,
            and you feel instinctively that they are not friendly.",
        d_to [;
            print "You clamber down the rope, straight into a patrol of guards who capture you!^";
            Capture();
            rtrue;
        ],
        u_to Spur_of_Rock,
        cant_go "Up and down are it, quite frankly.",
  has   light;

Object  -> hemp_rope "hemp rope"
  with  name 'hemp' 'rope',
        before [;
            Swing:
                give self general;
                "You set the rope swinging back and forth. Very exhilarating!";
        ],
  has   scenery;

Object  -> yellow_daisy "yellow daisy"
  with  name 'yellow//p' 'daisy',
        initial "A single yellow daisy grows from a little tuft of grass 
            on the crags.",
        before [;
            Take:
                if( self has moved || hemp_rope has general )
                    rfalse;
                "You stretch out your hand, but cannot quite reach the daisy, such is the size of the overhang.";
        ],
        after [;
            Take:
                if( self has general )
                    rfalse;
                give self general;
                "Aided by the swinging of the rope, you pick the daisy.";
            Wear:
                "You tuck the daisy behind your ear, almost out of sight.";
        ],
  has   clothing scored;

Object  Valley_Prospect "Valley Prospect"
  with  description "The geography is strangely familiar here. You are 
            on a natural shelf on the hillside, overlooking the valley
            below. An all-but invisible cleft in the rock leads to a cave
            to the southwest, and the slope can safely be traversed to
            the east.^^It is a brightly moonlit night, cool as in late
            spring. There will be a sharp frost tomorrow. Down in the
            valley, a great cartwheel circle of camp fires illuminate
            some dark shapes, perhaps crude encampments. There is no
            other sign of civilisation.",
        e_to Roman_Villa,
        sw_to Murky_Cave,
        cant_go "The hillside is too steep.",
  has   light;

Object  Roman_Villa "Roman Villa"
  with  name 'timber' 'door',
        description "You stand beside a Roman villa, whose columns cast 
            shadows of an Empire in the moonlight. It is thirty years
            since the fall of the West, and Britain is cut off from
            the continent, where soon even the existence of the Anglis
            will become a myth. This villa has survived the pestilence
            and devastation of the civil war, and although it must be
            a couple of centuries old, it is still standing. From here
            you could cut across the hillside east to west, or slip in
            through a none-too-secure timber door.",
        s_to Cloister,
        e_to Stream,
        w_to Valley_Prospect,
  has   light;

Object  Stream "Stream"
  with  name 'river' 'road' 'stream' 'roof',
        description "A stream runs past the villa and cascades down the 
            hillside here, to join the river far below, near the Roman
            road which will, in 1500 years time, become the motorway
            which so spoils the view from your back garden. A rough
            ladder of wood lashed together with leather rests against
            the villa at the lowest point of the tiled roof.",
        u_to "You climb the ladder and are about to jump onto the roof 
            when you catch sight of a man swaddled in animal furs,
            spreadeagled over the tiles, looking down into the central
            courtyard of the villa. He has a nasty-looking sword and
            since he hasn't seen you, you quietly shin back down the
            ladder again.",
        w_to Roman_Villa,
  has   light;

Object  -> ladder "ladder"
  with  name 'ladder',
  has   scenery;

Object  Cloister "Atrium Cloister"
  with  name 'timber' 'door',
        description "The shadowy cloisters of the villa's atrium. You 
            disturb a fieldmouse, and hear the scratching of tiny claws
            on stone. Moonlight filters in from the central courtyard
            to the south, and a rotten timber door leads back north.",
        n_to Roman_Villa,
        s_to Atrium,
  has   light;

Object  -> summoning_horn "summoning horn"
  with  name 'horn' 'summon' 'on' 'pole',
        initial "Resting against one pillar is a horn made from a tusk, 
            mounted on a long spear-like pole.",
        before [;
            Blow:
                print "You blow the horn, and it gives a great clarion call out to the hills which is promptly answered by a large number of annoyed guardsmen.^";
                Capture();
                rtrue;
        ],
        rod_assoc rod_of_language,
  has   hidden_rod;

Rod     rod_of_language "Rod of Language"
  with  name 'rod' 'of' 'language',
        rod_action [;
            if( second ~= player )
                "Nothing obvious happens.";
            KnowLanguages = true;
            "Nothing obvious happens. You curse eloquently in Celtic.";
        ];

Object  Atrium "Atrium"
  with  description "The central well of the villa, ringed with columns. 
            On the north side is a cloister and stone-linteled doorways
            lead east, south and west. The central floor area is commanded
            by a beautiful Roman mosaic, quartered into individual scenes,
            around the edges of which are vents from the hypocaust. There
            is no roof over the courtyard, and the atrium is lit by
            moonlight.",
        n_to Cloister,
        s_to Entrance,
        e_to Triclinium,
        w_to Guard_Quarters,
  has   light;

Object  -> ancient_mosaic "mosaic"
  with  name 'mosaic',
        description "The four quarters show white-beards in the Forum, 
            farmers in Italian fields, a military fortification and a
            grain ship unloading at Ossia.",
  has   scenery;

Object  Guard_Quarters "Guard Quarters"
  with  name 'armour' 'spears' 'hides' 'blanke',
        description "Whatever function this spacious room once had, 
            now it is evidently home to military men, for it is filled
            with crude armour, spears, rough blankets which are little
            more than animal hides. Fortunately for you the guards
            are absent.",
        e_to Atrium,
  has   light;

Object  -> bone_die "crude bone die"
  with  name 'die' 'dice' 'bone',
        description "A crude six-sided die made from animal bone.",
        after [;
            Drop:
                print_ret "Alea iacta est. It comes up ", (number) random(6), ".";
        ],
  has   ;

Object  -> sandals "leather sandals"
  with  name 'sandals' 'shoes' 'leather',
        article "a pair of",
  has   clothing;

Object  Triclinium "Triclinium"
  with  description "What was once the villa's triclinium, or kitchen, 
            is now deserted and long since ransacked for metal. (Nobody
            lives on their own in this century, and this villa wouldn't
            be easy to defend.)  A doorway to the west gives onto the
            atrium.",
        before [;
            Go:
                if( noun ~= d_obj )
                    rfalse;
                if( kitchen_well hasnt open )
                    rfalse;
                if( kitchen_well has general ) {
                    print "Aware of the dangers, you very carefully lower yourself...^";
                    Achieved(SCORE_TRICLINIUM);
                    rfalse;
                }
                deadflag = DIED;
                "Having no reason to suppose the well is very deep, you 
                 cheerfully lower yourself in. This turns out to be
                 something of a misjudgement and you soon lose your
                 grip and fall, endlessly banging against the walls
                 until finally...";
        ],
        w_to Atrium,
        d_to kitchen_well,
  has   light;

Object  -> kitchen_well "kitchen well"
  with  name 'well' 'hinged' 'wooden' 'cover',
        description "The well has a hinged wooden cover. You can't tell 
            by looking how deep it is.",
        when_open "The hinged wooden cover is raised, revealing a circular 
            well about a yard in diameter. It is very dark inside.",
        door_dir d_to,
        door_to Inside_Well,
        when_closed "In one corner is a hinged wooden cover of some kind, which is shut.",
        after [;
            Receive:
                remove noun;
                if( noun has light ) {
                    give kitchen_well general;
                    "It plummets out of sight, until even the light vanishes in the distance. This must be an extremely deep well.";
                }
                if( noun ~= historical_pamphlet )
                    StartTimer(kitchen_well,2);
                "It drops into the well-shaft.";
        ],
        number -1,
        time_left 0,
        time_out [;
            if( location == Triclinium ) {
                give kitchen_well general;
                print "^You hear a splash from far down in the depths of the well.^";
            }
            self.number = 0;
        ],
        daemon [;
            self.number ++;
            switch( self.number ) {
                1: "^From up on the hill you hear the blowing of a horn.";
                2: "^Horses are approaching the villa, coming down the hill.";
                3: "^The sound of horses draws nearer.";
                4: 
                   if( location == Entrance ) {
                     print "^Horsemen suddenly gallop into the courtyard and you are soon ringed by guards!^";
                     Capture();
                     StopDaemon(self);
                     rtrue;
                   }
                   "^There is definitely activity outside the villa.";
            }
            if( self.number >= 5 && (location == Entrance || location == Roman_Villa)) {
                if( location == Entrance )
                    print "^You walk straight into the guards.^";
                else if( location == Roman_Villa )
                    print "^You walk straight into the guards at the back door.^";
                Capture();
                StopDaemon(self);
                rtrue;
            }
            if( self.number == 7 ) {
                print "^There is sudden activity as the guards make a thorough search of the villa, just to be on the safe side. ";
                if( location == Hypocaust )
                    "Luckily, they don't even think of looking down in here.^";
                else {
                    if( location == Inside_Well )
                        print "Unfortunately, they do think of looking into the kitchen well, and drag you out onto the triclinium floor.^";
                    else
                        print "The virtue of doing so is demonstrated when they find you without difficulty.^";
                    Capture();
                    StopDaemon(self);
                    rtrue;
                }
            }
            if( self.number > 7 && location == Triclinium ) {
                print "^An industrious-looking slave (who cooks for the horsemen) is rather surprised to see you, but with creditable presence of mind he summons the guard at once.^";
                Capture();
                StopDaemon(self);
            }
            if( location == Hypocaust ) {
                switch( self.number ) {
                    8: "^You hear men walking across the mosaic just above your head.";
                    9: print "^A voice, which can just be heard through the vent, says ";
                       if( KnowLanguages )
                           "~Welcome, great masters. We are convened in secret conclave to decide the fate of Britain. Lament, for the old ways are passing!  There will be a dark age, a collapse into many monarchies, and any man who can hold a town will call himself King and heir to Vortigern.~";
                        else
                            "something in Anglo-Saxon or Celtic (hard to say which).";
                   10: print "^A younger, more impatient voice, says ";
                       if( KnowLanguages )
                           "~This is an opportunity!  We must be the power behind the thrones, we will in secret unite and be the true force of the land...~";
                       else
                           "something else.";
                   11: print "^There is cynical laughter for a moment, and a third voice interjects ";
                       if( KnowLanguages )
                           "~And who will lead this, this unity?  We take all sides and none, you know that. There are potent druids in the tents of each general down there in the rabble. They'd kill each other without hesitation on the battlefield.~";
                       else
                           "something.";
                   12: print "^The first voice speaks once more. ";
                       if( KnowLanguages )
                           "~Far worse is to come. The jumped-up kings and their new men will no longer believe in the ancient powers. Already I can feel the druidic magic weakening away, and we are perilously close to the nexus now. In the valleys below, armies wait to fight the decisive battle. Whichever side wins, they will no longer need us. In a matter of hours, the power will be broken forever.~";
                       rtrue;
                   13: print "^After a sombre silence, a much older voice says ";
                       if( KnowLanguages )
                           "~You did right to call us together. There is a path prepared for us, but the sacrifice will be terrible. We must concentrate all the remaining druidical energy into this sacred hill, that it may last through all time, and we must ensure that druids live on this site forever and keep the faith.~";
                       else
                           "something wise. It really is a pity you can't understand them.";
                   14: print "^The younger voice speaks again. ";
                       if( KnowLanguages )
                           "~For now, yes, for a century or two, yes, but what then?  They will forget the old ways, they will no longer truly be druids.~";
                       rtrue;
                   15: print "^The older voice replies triumphantly. ";
                       if( KnowLanguages )
                           "~This is what must be done. They will forget, so we must give them a substitute. We shall accurse them to always seek after this magical stone, which it will be all but impossible for them to find. It will take the form of whatever they most desire. In six hundred years, by my prophecy, religious wars in the East will capture men's hearts, and then they will think it is the Holy Grail, the cup which held the blood of Christ. A thousand years later they will think it is -~  But at this moment the voices move away.";
                       rtrue;
                }
            }
        ],
  has   door container openable static;

Object  Inside_Well "Inside Well"
  with  name 'air',
        description "You are clinging perilously to the walls of the deep and 
            dangerous well. You can hardly see a thing, even with the
            aid of the torch, but curiously enough you feel warm air
            blowing across you from the west.",
        before [;
            Go:
                rfalse;
            default: "There's not enough room in this confined space to do anything much!";
        ],
        w_to Hypocaust,
        u_to Triclinium,
        cant_go "Too risky. There might not be anything that way, for all you know.",
  has   ;

Object  Hypocaust "Hypocaust"
  with  name 'fires' 'heating' 'log' 'logs',
        description "This very low almost-cellar underneath the atrium is a 
            tiny access space for log fires which are kept going to
            provide a form of central heating for the villa. They are
            lit and going away nicely, interestingly. It isn't very
            warm by twentieth-century standards, but you have to admit
            it works. A tiny amount of light filters in from the vents
            in the roof, but nowhere near enough to see by. The only
            way in seems to be the way you came.",
        e_to Inside_Well,
        cant_go "The log fires are hot, and anyway the only way out large enough for you is back east.",
  has   light;

Object  -> hypocaust_vents "hypocaust vents"
  with  name 'vents',
        before [;
            Examine, Search:
                if( kitchen_well.number > 7 )
                    "Four men in druidic furs are standing on the corners of the mosaic, in some kind of sinister conference. This old villa must be some kind of rendezvous point. Between the men a bluish stone rests on the mosaic floor.";
                "You see across the mosaic floor of the moonlit atrium.";
        ],
  has   scenery;

Object  Entrance "Entrance"
  with  description "A well-flagstoned forecourt at the villa entrance, 
            surrounded by lesser wooden buildings. Some of these are in
            use as stables, and figures of men slip between them. You hide
            from sight, not wishing to draw attention to yourself. Up
            on the hill, where Meldrew House will one day be built,
            men on horseback can be seen circling the access track. For
            a deserted villa, this is certainly well-guarded.",
        before [;
            Go:
                if( noun == s_obj || noun == sw_obj || noun == se_obj ) {
                    print "You walk out into the courtyard and are immediately 
                        set upon by guards, who drag you away.^";
                    Capture();
                    rtrue;
                }
                rfalse;
        ],
        after [;
            if( kitchen_well.number == 0 ) {
                StartDaemon(kitchen_well);
            }
        ],
        n_to Atrium,
  has   light scored;

Object  Rough_Tent "Rough Tent"
  with  name 'hides',
        description "Well, here you are, imprisoned again. This time the 
            cell is a tent of stitched animal hides, perhaps twenty feet
            in diameter, supported by a central pole. You have no idea
            where the entrance is, because you were blindfolded on the
            way in. There is at least a dim light from the camp-fires
            outside the tent.",
        cant_go "This tent doesn't seem to have an exit, either.",
  has   light scored;

Object  -> tent_pole "tent pole"
  with  name 'pole' 'central',
        after [;
            Take:
                give self general;
                print "As you uproot the tent-pole, the whole tent collapses 
                    ignominiously, the animal hides falling about your
                    head. The hides are heavy and weigh you down, but
                    you just manage to crawl out...^";
                StartTimer(Encampment, 10);
                PlayerTo(Encampment, 2);
                rtrue;
        ],
        time_left 0,
        time_out [;
            give tent_pole ~static;
            move saxon_spy to Rough_Tent;
            StartTimer(saxon_spy, 4);
            "^Without warning a Saxon spy (or, as the guards would no doubt put 
             it, another Saxon spy) is thrown bodily into the tent,
             careering into the central pole and slumping unconscious on
             the ground as a result. For a moment the whole tent rocks
             with the force - the pole was nearly broken.^";
        ],
        rod_assoc rod_of_returning2,
  has   concealed static hidden_rod;

Rod     rod_of_returning2 "Rod of Returning"
  with  name 'rod' 'of' 'returning',
        article "the",
        rod_action transport_player;

Object  saxon_spy "unconscious Saxon spy"
  with  name 'saxon' 'spy',
        description "He is dishevelled, wiry and unconscious. He is also 
            covered with dust and leaves, as if he has been lying down
            on his front outdoors for hours.",
        initial "An unconscious Saxon spy is slumped on the ground. 
            Occasionally faint noises can be heard from him.",
        before [; 
            Search:
                "Not only are the guards better at searching suspects 
                 than you, they also did so first.";

            WakeOther: "The spy is too deeply unconscious.";
        ],
        time_left 0,
        time_out [;
            if( location ~= Rough_Tent )
                rfalse;
            if( parent(self) == Rough_Tent ) {
                remove self;
                StartTimer(saxon_spy, 5);
                "^Men arrive to drag the Saxon spy away to some unguessable
                 grisly fate. They grimace at you as if to say, ~You're next.~";
            }
            deadflag = DIED;
            "^The men arrive once more and this time they securely grab you 
             by the arms. If you were an anthropologist, you'd be
             fascinated by the ritual in which you are bound up, then
             garotted with a cord and laid into a peat swamp. Instead,
             your last thought is to remember the celebrated body they
             found when they drained the marshes in 1974 to build a
             motorway service station.";
        ],
  has   animate;

Object  Encampment "Encampment"
  with  name 'fires' 'camp' 'dolmen',
        description "Luckily for you the tent, now collapsed, was hardly 
            being watched at all, because all the attention is diverted
            to a ghastly ritual being carried out over beyond the
            camp-fires. But your luck cannot hold for long. You are so
            surrounded by hostile territory that you simply have nowhere
            to run or hide. Things are, not to put too fine a point on
            it, looking grim.",
        e_to [;
            Go:
                if( noun == e_obj ) {
                    if( sandals hasnt worn ) 
                        "Walk across those hot coals!  You must be joking.";
                    print "You cross the hot coals nimbly in your sandals.^";
                    return dolmen;
                }
                rfalse;
        ],
        time_left 0,
        time_out [;
            if( game_section ~= ANCIENT_BRITAIN )
                rfalse;
            deadflag = DIED;
            "^It had to happen sooner or later. One of the druids looks 
             round, sees you and raises an instant alarm. It is absolutely
             hopeless and you are dragged away screaming to your fate.";
        ],
        cant_go "Far, far too risky.",
  has   light scored;

Object  -> dolmen "dolmen"
  with  name 'dolmen' 'coals' 'hot' 'rock',
        describe "An erratic glacial rock, which seems in the feverish 
            firelight to make a natural dolmen, can be made out to the
            east, on the other side of an almost-extinguished campfire
            of hot coals.",
        door_dir initial,
        door_to Beneath_Dolmen,
  has   door open;   
  !! R7 bug: you can take the dolmen!

Object  Beneath_Dolmen "Beneath Dolmen"
  with  name 'dolmen',
        description "You stand beside the eerie dolmen, surrounded by the 
            terrible noises and incantations of a druidic rite.",
        w_to Encampment,
        cant_go "Far, far too risky.",
  has   light scored;

Object  -> bluish_stone "bluish stone"
  with  name 'stone' 'blue//p' 'bluish',
        initial "Resting on top of the dolmen is a polished blue stone.",
        rod_assoc fifty_franc_note,
  has   hidden_rod scored;

Object  fifty_franc_note "fifty-franc note"
  with  name 'fifty' 'franc' 'note' 'francs',
  has   ;

[ Capture o;
    if (kitchen_well.number < 8) {
        deadflag = DIED;
        "^The guardsmen grab your head by the chin and examine you, 
            but take you for a plain thief and immediately put you to
            the sword. A pity they did not realise your importance,
            but then they are only guards, and anyway if they had they
            would probably just have done you in more unpleasantly.";
    }
    print "^Suddenly you are thrust before a druidical figure. He seems to 
        recognise you, and is troubled. Perhaps because of this, your
        life is saved and you are taken away into custody, down in the
        valley among the army encampments.^";
    StartTimer(tent_pole, 4);
    StopDaemon(kitchen_Well);

    for( o = child(player): o: o = sibling(o) )
        if( o hasnt worn )
            remove o;
    PlayerTo(Rough_Tent, 2);
     rtrue;
    

];
