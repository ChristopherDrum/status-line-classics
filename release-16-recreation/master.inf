[ StartMasterGame o sib ;
    game_section = T_ANCIENT_BRITAIN;
    o = child(player);
    give daisy_chain ~worn;
    while( o ~= nothing ) {
        sib = sibling(o);
        give o ~worn;
        move o to Infinity_Symbol;
        o = sib;
    }
    move historical_pamphlet to player;
    PlayerTo(Murky_Cave);
    rtrue;
];

[ Capture o s ;
    if( CouncilCnt < 8 ) {
        deadflag = DIED;
        "^The guardsmen grab your head by the chin and examine you, but take you for a plain thief and 
         immediately put you to the sword. A pity they did not realise your importance, but then they are only
         guards, and besides they would probably only have done you in more unpleasantly.";
    }
    print "^Suddenly you are thrust before a druidical figure. He seems to recognise you, and is troubled. 
        Perhaps because of this, your life is saved and you are taken away into custody, down in the valley among
        the army encampments.^";

    StartTimer(tent_pole,4);
    for( o = child(player) : o ~= nothing : o = s ) {
        s = sibling(o);
        if( o hasnt worn ) 
            remove o;
        o = s;
    }
    CouncilCnt = 0;
    PlayerTo(Rough_Tent);
    rtrue;
];

Object Murky_Cave "Murky Cave"  
  with  w_to Spur_of_Rock,
        ne_to Valley_Prospect,
        initial [; StartDaemon(self); ],
        description "This is a murky hillside cave, whose mouth opens to the northeast, though a tight squeeze might 
            lead a little way west. Outside is a clear starry night. There is no traffic noise, and the air
            smells fresh.",
        daemon [ cnt;
            if( game_section ~= T_ANCIENT_BRITAIN ) 
                StopDaemon(self);
            cnt = CouncilCnt;
            if( cnt > 0 ) 
                ++CouncilCnt;

            ! I don't think the following is ever printed:
            if( cnt == 1 ) "^From up on the hill you hear the blowing of a horn.";
            if( cnt == 2 ) "^Horses are approaching the villa, coming down the hill.";
            if( cnt == 3 ) "^The sound of horses draws nearer.";
            if( cnt == 4 ) {
                if( location == Entrance ) {
                    print "^Horsemen suddenly gallop into the courtyard and you are soon ringed by guards!^";
                    Capture();
                } else {
                    "^There is definitely activity outside the villa.";
                }
            }
            if( cnt >= 5 ) {
                if( location == Entrance ) {
                    print "^You walk straight into the guards.^";
                    Capture();
                }
                if( location == Roman_Villa ) {
                    print "^You walk straight into the guards at the back door.^";
                    Capture();
                }
            }
            if( cnt == 7 ) {
                print "^There is sudden activity as the guards make a thorough precautionary search of the villa. ";
                if( location == Hypocaust ) {
                    "Luckily, they don't even think of looking down in here.";
                }
                if( location == Inside_Well ) {
                    print "Unfortunately, they do think of looking into the kitchen well, and drag you out onto the 
                        triclinium floor.^";
                }
                if( location == Murky_Cave or Spur_of_Rock or Hanging_on_Rope ) 
                    print "With the kind of zeal which really marked early Dark Age guarding, they look even here.^";
                else if( location ~= Inside_Well )
                    print "The virtue of doing so is demonstrated when they find you without difficulty.^";
                Capture();
            }
            if( cnt <= 7 ) 
                rfalse;

            if( location == Triclinium ) {
                print "^An industrious-looking slave (who cooks for the horsemen) is rather surprised to see you, 
                    but with creditable presence of mind he summons the guard at once.^";
                Capture();
            }
            if( location ~= Hypocaust ) 
                rfalse;
            if( cnt == 8 ) 
                "^You hear men walking across the mosaic just above your head.";
            if( cnt == 9 ) {
                print "^A voice, which can just be heard through the vent, says ";
                if( KnowLanguages == false ) {
                    "something in Anglo-Saxon or Celtic (hard to say which).";
                }
                "~Welcome, great masters. We are convened in secret conclave to decide the fate of Britain. 
                 Lament, for the old ways are passing!  There will be a dark age, a collapse into many monarchies,
                 and any man who can hold a town will call himself King and heir to Vortigern.~";
            }
            if( cnt == 10 ) {
                print "^A younger, more impatient voice, says ";
                if( KnowLanguages == false ) "something else.";
                "~This is an opportunity!  We must be the power behind the thrones, we will in secret unite and 
                 be the true force of the land...~";
            }
            if( cnt == 11 ) {
                print "^There is cynical laughter for a moment, and a third voice interjects ";
                if( KnowLanguages == false ) "something.";
                "~And who will lead this, this unity?  We take all sides and none, you know that. There are 
                 potent druids in the tents of each general down there in the rabble. They'd kill each other in
                 a second come the battle.~";
            }
            if( cnt == 12 ) {
                print "^The first voice speaks once more. ";
                if( KnowLanguages == false ) {
                    new_line;
                    rtrue;
                }
                "~Far worse is to come. The jumped-up kings and their new men will no longer believe in the 
                 ancient powers. Already I can feel the druidic magic weakening away, and we are perilously close
                 to the nexus now. In the valleys below, armies wait to fight the decisive battle. Whichever side
                 wins, they will no longer need us. In a matter of hours, the power will be broken forever.~";
            }
            if( cnt == 13 ) {
                print "^After a sombre silence, a much older voice says ";
                if( KnowLanguages == false ) "something wise. It really is a pity you can't understand them.";
                "~You did right to call us together. There is a path prepared for us, but the sacrifice will be 
                 terrible. We must concentrate all the remaining druidical energy into this sacred hill, that it may
                 last through all time, and we must ensure that druids live on this site forever and keep the faith.~";
            }
            if( cnt == 14 ) {
                print "^The younger voice speaks again. ";
                if( KnowLanguages == false ) {
                    new_line;
                    rtrue;
                }
                "~For now, yes, for a century or two, yes, but what then?  They will forget the old ways, they 
                 will no longer truly be druids.~";
            }
            if( cnt == 15 ) {
                print "^The older voice replies triumphantly. ";
                if( KnowLanguages == false ) {
                    new_line;
                    rtrue;
                }
                "~This is what must be done. They will forget, so we must give them a substitute. We shall accurse 
                 them to always seek after this magical stone, which it will be all but impossible for them to
                 find. It will take the form of whatever they most desire. In six hundred years, by my prophecy,
                 religious wars in the East will capture men's hearts, and then they will think it is the Holy
                 Grail, the cup which held the blood of Christ. A thousand years later they will think it is -~
                 But at this moment the voices move away.";
            }
            rfalse;
        ],
        section T_ANCIENT_BRITAIN,
  has   ;

Object -> flaming_torch "flaming torch" 
  with  name 'flaming' 'torch',
        initial "Hanging up on an iron bracket is a flaming torch.",
  has   light;

Object -> historical_pamphlet "historical pamphlet"     
  with  name 'historical' 'pamphlet',
        sw_to n_obj,
        description "It is a summary of the writings of a polemical monk called Gildas, and relates to events 
            following the fall of the tyrant Vortigern at the turn of the fifth century A.D. After dark decades
            of pillage by Saxon mercenaries, Britain was reunited by Ambrosius Aurelianus and then defended at the
            Battle of Badon Hill, some time in the early years of the sixth century.^^This remarkable victory was
            of no religious significance, so Gildas does not dwell upon it. He tends only to mention bad kings
            and leaders, and then chiefly to insult them. (Talking about Gildas is an excellent way to annoy an
            Anglo-Saxon historian.)  Nobody even knows where Badon Hill is any more, or anything else about it,
            although some students of Welsh poetry believe Ambrosius was the source of the legend of King Arthur.",
        article "an",
  has   ;

Object Spur_of_Rock "Spur of Rock"      
  with  e_to Murky_Cave,
        d_to Hanging_on_Rope,
        description "An uncomfortable spur of rock on the hillside crags. The only natural access to this spot is a 
            crevice in the hill to the east, but there is also a securely fastened rope hanging down into the 
            darkness.",
        cant_go "The crags are too precipitous.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> hanging_rope "hanging rope"   
  with  name 'hanging' 'rope',
        before [;
            Climb: << Go d_obj >>;
        ],
  has   scenery;

Object Hanging_on_Rope "Hanging on Rope"        
  with  name 'patrol' 'men',
        before [;
            Swing: if( noun ~= hemp_rope ) rfalse;
            give hemp_rope general;
            "You set the rope swinging back and forth. Very exhilarating!";
        ],
        after [;
            Drop:
                remove(noun);
                print_ret (The) noun, " tumbles end over end into the valley below.";
        ],
        u_to Spur_of_Rock,
        d_to [;
            print "You clamber down the rope, straight into a patrol of guards who capture you!^";
            Capture();
            rtrue;
        ],
        description "You are hanging perilously on a hemp rope, suspended from an overhang of rock above, which 
            drops down into the darkness of the valley. On the tracks below, patrols of men can be heard, and you
            feel instinctively that they are not friendly.",
        cant_go "Up and down are it, quite frankly.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> hemp_rope "hemp rope" 
  with  name 'hemp' 'rope',
  has   scenery;

Object -> yellow_daisy "yellow daisy"   
  with  name 'yellow' 'daisy',
        before [;
            if( self has moved || hemp_rope has general ) rfalse;
            "You stretch out your hand, but cannot quite reach the daisy, such is the size of the overhang.";
        ],
        after [;
            Wear: "You tuck the daisy behind your ear, almost out of sight.";
            Take:
                if( self hasnt general ) {
                    give self general;
                    "Aided by the swinging of the rope, you pick the daisy.";
                }
                rfalse;
        ],
!        u_to DaisyUp,
        initial "A single yellow daisy grows from a little tuft of grass on the crags.",
  has   clothing scored;

Object Valley_Prospect "Valley Prospect"        
  with  e_to Roman_Villa,
        sw_to Murky_Cave,
        description "The geography is strangely familiar here. You are on a natural shelf on the hillside, 
            overlooking the valley below. An all-but invisible cleft in the rock leads to a cave to the southwest,
            and the slope can safely be traversed to the east.^^It is a brightly moonlit night, cool as in late
            spring. There will be a sharp frost tomorrow. Down in the valley, a great cartwheel circle of camp
            fires illuminate some dark shapes, perhaps crude encampments. There is no other sign of civilisation.",
        cant_go "The hillside is too steep.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object Roman_Villa "Roman Villa"        
  with  name 'timber' 'door',
        s_to Atrium_Cloister,
        e_to Villa_Stream,
        w_to Valley_Prospect,
        description "You stand beside a Roman villa, whose columns cast shadows of an Empire in the moonlight. 
            It is thirty years since the fall of the West, and Britain is cut off from the continent, where soon even
            the existence of the Anglis will become a myth. This villa has survived the pestilence and devastation
            of the civil war, and although it must be a couple of centuries old, it is still standing. From here
            you could cut across the hillside east to west, or slip in through a none-too-secure timber door.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object Villa_Stream "Stream"  
  with  name 'river' 'road' 'stream' 'roof',
        before [;
            Swim: "Too shallow for comfort.";
            Drink: "Deliciously clear and unpolluted.";
        ],
        w_to Roman_Villa,
        u_to "You climb the ladder and are about to jump onto the roof when you catch sight of a man swaddled in 
            animal furs, spreadeagled over the tiles, looking down into the central courtyard of the villa. He
            has a nasty-looking sword and since he hasn't seen you, you quietly shin back down the ladder again.",
        description "A stream runs past the villa and cascades down the hillside here, to join the river far below, 
            near the Roman road which will, in 1500 years time, become the motorway which so spoils the view from
            your back garden. A rough ladder of wood lashed together with leather rests against the villa at the
            lowest point of the tiled roof.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> rough_ladder "rough ladder"   
  with  name 'rough' 'ladder',
        before [; Climb: << Go u_obj >>; ],
  has   scenery;

Object Atrium_Cloister "Atrium Cloister"        
  with  name 'timber' 'door',
        n_to Roman_Villa,
        s_to Atrium,
        description "The shadowy cloisters of the villa's atrium. You disturb a fieldmouse, and hear the 
            scratching of tiny claws on stone. Moonlight filters in from the central courtyard to the south,
            and a rotten timber door leads back north.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> summoning_horn "summoning horn"       
  with  name 'horn' 'summoning' 'on' 'pole',
        before [;
            Blow:
                print "You blow the horn, and it lets a great clarion call out to the hills. ";
                if( CouncilCnt >= 7 ) {
                    print "The guardsmen, all over the villa, find you very quickly.^";
                    Capture();
                } else {
                    print "A call which is rapidly answered as...^";
                    CouncilCnt = 7;
                }
                rtrue; 
        ],
        initial "Resting against one pillar is a horn made from a tusk, mounted on a long spear-like pole.",
        rod_assoc rod_of_language,
  has   hidden_rod;

Object Atrium "Atrium"  
  with  n_to Atrium_Cloister,
        s_to Entrance,
        e_to Guard_Quarters,
        w_to Triclinium,
        description "The central well of the villa, ringed with columns. On the north side is a cloister and 
            stone-linteled doorways lead east, south and west. The central floor area is commanded by a beautiful Roman
            mosaic, quartered into individual scenes, around the edges of which are vents from the hypocaust. There
            is no roof over the courtyard, and the atrium is lit by moonlight.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> ancient_mosaic "mosaic"       
  with  name 'mosaic',
        description "The four quarters show white-beards in the Forum, farmers in Italian fields, a military 
            fortification and a grain ship unloading at Ossia.",
  has   scenery;

Object Guard_Quarters "Guard Quarters"  
  with  name 'armour' 'spears' 'hides' 'blankets',
        w_to Atrium,
        description "Whatever function this spacious room once had, now it is evidently home to military men, 
            for it is filled with crude armour, spears, rough blankets which are little more than animal
            hides. Fortunately for you the guards are absent.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> bone_die "crude bone die"     
  with  name 'die' 'dice' 'bone',
        after [;
            Drop:
                print_ret "Alea iacta est. It comes up ", EnglishNumber(random(6)), ".";
        ],
        description "A crude six-sided die made from animal bone.",
  has   ;

Object -> leather_sandals "leather sandals"     
  with  name 'sandals' 'shoes' 'leather',
        article "a pair of",
  has   clothing;

Object Triclinium "Triclinium"  
  with  e_to Atrium,
        d_to [;
            if( kitchen_well hasnt open )
                "The well is covered.";
            if( kitchen_well has general ) {
                print "Aware of the dangers, you very carefully lower yourself...^";
                Achieved(SCORE_WELL);
                return kitchen_well;
            }
            deadflag = DIED;
            "Having no reason to suppose the well is very deep, you cheerfully lower yourself in. This turns out 
             to be something of a misjudgement and you soon lose your grip and fall, endlessly banging against
             the walls until finally...";
        ],
        description "What was once the villa's triclinium, or kitchen, is now deserted and long since ransacked for 
            metal. (Nobody lives on their own in this century, and this villa wouldn't be easy to defend.)
            A doorway to the east gives onto the atrium.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> kitchen_well "kitchen well"   
  with  name 'well' 'hinged' 'wooden' 'cover',
        after [;
            Receive:
                remove noun;
                if( noun has light ) {
                    give kitchen_well general;
                    "It plummets out of sight, until even the light vanishes in the distance. This must be an 
                     extremely deep well.";
                }
                if( noun ~= historical_pamphlet ) {
                    StartTimer(self,2);
                }
                print_ret (The) noun, " drops into the well-shaft.";
        ],
        door_to Inside_Well,
        when_closed "In one corner is a hinged wooden cover of some kind, which is shut.",
        door_dir d_to,
        when_open "The hinged wooden cover is raised, revealing a circular well about a yard in diameter. 
            It is very dark inside.",
        description "The well has a hinged wooden cover. You can't tell by looking how deep it is.",
        time_left 0,
        time_out [;
            if( player notin Triclinium ) rfalse;
            give kitchen_well general;
            trigger_quotation(9);
            "^You hear a splash from far down in the depths of the well.";
        ],
  has   container door openable static;

Object Inside_Well "Inside Well"        
  with  name 'air',
        before [;
            Look, Go, Examine:
                rfalse;
            default:
                "In this confined space there is little room for action.";
        ],
        e_to Hypocaust,
        u_to Triclinium,
        description "You are clinging perilously to the walls of the deep and dangerous well. You can hardly see 
            a thing, even with the aid of the torch, but curiously enough you feel warm air blowing across you
            from the east.",
        cant_go "Too risky. There might not be anything that way, for all you know.",
        section T_ANCIENT_BRITAIN,
  has   ;

Object Hypocaust "Hypocaust"    
  with  name 'fires' 'heating' 'log' 'logs',
        w_to Inside_Well,
        description "This very low almost-cellar underneath the atrium is a tiny access space for log fires which are 
            kept going to provide a form of central heating for the villa. They are lit and going away nicely,
            interestingly. It isn't very warm by twentieth-century standards, but you have to admit it works.^^A
            tiny amount of light filters in from the vents in the roof; just enough to see by. The only way in
            seems to be the way you came.",
        cant_go "The log fires are hot, and anyway the only way out large enough for you is back west.",
        section T_ANCIENT_BRITAIN,
  has   light;

Object -> vents "vents" 
  with  name 'hypocaust' 'vents',
        before [;
            Examine, Search:
                if( CouncilCnt > 7 ) 
                    "Four men in druidic furs are standing on the corners of the mosaic, in some kind of sinister 
                     conference. This old villa must be some kind of rendezvous point. Between the men a bluish
                     stone rests on the mosaic floor.";
                "You see across the mosaic floor of the moonlit atrium.";
        ],
        article "the",
  has   scenery;

Object Entrance "Entrance"      
  with  before [;
            Go:
                if( noun ~= s_obj or sw_obj or se_obj )
                    rfalse;
                print "You walk out into the courtyard and are immediately set upon by guards, who drag you away.^";
                Capture();
                rtrue;
        ],
        after [;
            if( CouncilCnt == 0 ) 
                CouncilCnt = 1;
        ],
        n_to Atrium,
        description "A well-flagstoned forecourt at the villa entrance, surrounded by lesser wooden buildings. 
            Some of these are in use as stables, and figures of men slip between them. You hide from sight, not
            wishing to draw attention to yourself. Up on the hill, where Meldrew Hall will one day be built, men
            on horseback can be seen circling the access track. For a deserted villa, this is certainly well-guarded.",
        section T_ANCIENT_BRITAIN,
  has   light scored;

Object Rough_Tent "Rough Tent"  
  with  name 'hides',
        description "Well, here you are, imprisoned again. This time the cell is a tent of stitched animal hides, 
            perhaps twenty feet in diameter, supported by a central pole. You have no idea where the entrance is,
            because you were blindfolded on the way in. There is at least a dim light from the camp-fires outside
            the tent.",
        cant_go "This tent doesn't seem to have an exit, either.",
        section T_ANCIENT_BRITAIN,
  has   light scored;

Object -> tent_pole "tent pole" 
  with  name 'tent' 'pole' 'central',
        before [;
            Attack: "It won't break.";
            Push, Pull, Turn: "Its base is in the earth.";
        ],
        after [;
            Take:
                if( self has general ) {
                    rfalse;
                }
                give self general;
                print "As you uproot the tent-pole, the whole tent collapses ignominiously, the animal hides 
                    falling about your head. The hides are heavy and weigh you down, but you just manage to crawl
                    out...^";
                StartTimer(Encampment,10);
                PlayerTo(Encampment);
                rtrue;
        ],
        time_left 0,
        time_out [;
            print "^Without warning a Saxon spy (or, as the guards would no doubt put it, another Saxon spy) is 
                thrown bodily into the tent, careering into the central pole and slumping unconscious on the ground
                as a result. For a moment the whole tent rocks with the force - the pole was nearly broken.^";
            give tent_pole ~static;
            move unconscious_Saxon_spy to Rough_Tent;
            StartTimer(unconscious_Saxon_spy,5);
        ],
        rod_assoc rod_of_returning2,
  has   concealed static hidden_rod;

Object unconscious_Saxon_spy "unconscious Saxon spy"    
  with  name 'saxon' 'spy',
        before [;
            Search: "Not only are the guards better at searching suspects than you, they also did so first.";
        ],
        life [;
            WakeOther: "The spy is too deeply unconscious.";
            Attack, Kiss, ThrowAt: "Not an act worthy of a Meldrew.";
            Order, Answer, Ask, Give: "He's asleep.";
        ],
        initial "An unconscious Saxon spy is slumped on the ground. Occasionally faint noises can be heard from him.",
        description "He is dishevelled, wiry and unconscious. He is also covered with dust and leaves, as if he 
            has been lying down on his front outdoors for hours.",
        time_left 0,
        time_out [;
            if( location ~= Rough_Tent ) 
                rfalse;
            if( unconscious_Saxon_spy in Rough_Tent ) {
                print "^Men arrive to drag the Saxon spy away to some unguessable grisly fate. They grimace at you 
                    as if to say, ~You're next.~^";
                remove unconscious_Saxon_spy;
                StartTimer(unconscious_Saxon_spy,5);
                rtrue;
            }
            deadflag = DIED;
            print "^The men arrive once more and this time they securely grab you by the arms. If you were an 
                anthropologist, you'd be fascinated by the ritual in which you are bound up, then garotted with a
                cord and laid into a peat swamp. Instead, your last thought is of the celebrated body they found
                when they drained the marshes in 1974 to build a motorway service station.";
            rfalse;
        ],
  has   animate;

Object Encampment "Encampment"  
  with  name 'fires' 'camp' 'dolmen',
        e_to [;
            if( leather_sandals has worn ) {
                print "You cross the hot coals nimbly in your sandals.^";
                return dolmen;
            }
            "Walk across those hot coals!  You must be joking.";
        ],
        description "Luckily for you the tent, now collapsed, was hardly being watched at all, because all the 
            attention is diverted to a ghastly ritual being carried out over beyond the camp-fires. But your luck
            cannot hold for long. You are so surrounded by hostile territory that you simply have nowhere to run
            or hide. Things are, not to put too fine a point on it, looking grim.",
        cant_go "Far, far too risky.",
        time_left 0,
        time_out [;
            if( game_section ~= T_ANCIENT_BRITAIN ) 
                rfalse;
            deadflag = DIED;
            "^It had to happen sooner or later. One of the druids looks round, sees you and raises an instant 
             alarm. It is absolutely hopeless and you are dragged away screaming to your fate.";
        ],
        section T_ANCIENT_BRITAIN,
  has   light scored;

Object -> dolmen "dolmen"       
  with  name 'dolmen' 'coals' 'hot' 'rock',
        door_to Beneath_Dolmen,
        door_dir e_to,
        when_open "An erratic glacial rock, which seems in the feverish firelight to make a natural dolmen, can be 
            made out to the east, on the other side of an almost-extinguished campfire of hot coals.",
  has   door open static;

Object Beneath_Dolmen "Beneath Dolmen"  
  with  name 'dolmen',
        w_to Encampment,
        description "You stand beside the eerie dolmen, surrounded by the terrible noises and incantations of a 
            druidic rite.",
        cant_go "Far, far too risky.",
        section T_ANCIENT_BRITAIN,
  has   light scored;

Object -> bluish_stone "bluish stone"   
  with  name 'stone' 'blue' 'bluish',
        initial "Resting on top of the dolmen is a polished blue stone.",
        rod_assoc fifty_franc_note,
        devil_hint "I wouldn't wave that around if I were you!",
  has   scored hidden_rod;

Object fifty_franc_note "fifty-franc note"      
  with  name 'fifty' 'franc' 'note' 'francs',
  has   ;
