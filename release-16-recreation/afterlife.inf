Array demon_topics --> 
    2 'premonition' 'dream' 
        "~In the premonition, it is vital to get hold of the mascot for later use.~" 
    2 'torch' 'battery' 
        "~It's no use poking about on the attic floor for fresh torch batteries, because there aren't any.~" 
    3 'aunt' 'jemima' 'gloves' 
        "~Your Aunt Jemima is not the kind of woman to be distracted by music or frivolous presents, mark my words.~" 
    3 'romance' 'novel' 'marie' 
        "~Novels are, of course, invariably written by people using their real names, not disguised in any way.~" 
    3 'daisy' 'daisychain' 'daisies' 
        "~The thing about daisies is, they are the same all year round, not changing with the calendar.~" 
    'medicine' 
        "~Modern medicine bottles can even withstand falls from a great height. I should try heat if I were you.~" 
    'demijohn' 
        "~You can easily get a grip on the demijohn with your bare hands.~"
    'bottle'
        "~The answer's the same, whichever bottle you meant. That counts as your hint, I'm afraid. Not very fair, but then I am a demon.~" 
    'heaven'
        "The demon acknowledges the question, but cannot bring himself to speak of Heaven." 
    'hell'
        "~Hell?  Oh this isn't your eternal damnation, it's mine, having to answer all these wretched questions. No,
        Hell itself is a marvellous place, and it has an excellent health club too. Look forward to seeing you there.~"
    2 'card' 'postcard'
        "~Don't worry, there's nothing written there. No need to look at the problem in a fresh light.~" 
    3 'ghost' 'sir' 'joshua'
        "~History never repeats itself, so it's pointless looking him up in the book.~" 
    2 'prayer' 'isaac'
        "~The really interesting stuff is at the very start of his working life.~" 
    4 'museum' 'arcana' 'doktor' 'stein'
        "~Forget it, there's no way to return from the museum, and Doktor Stein's medicine has no antidote.~" 
    2 'merlyn' 'merlin'
        "~Merlyn doesn't really come into this. Try working on something else.~" 
    'hat'
        "~Literally, Merlyn's actual hat. Green pointy felt, I wouldn't wonder.~" 
    'book'
        "~The answer's the same, whichever book you meant. That counts as your hint, I'm afraid. Not very fair,
        but then I am a demon.~"
    'ebenezer'
        "~Ebenezer?  He's absolutely central to this. Concentrate on him, definitely.~" 
    3 'projector' 'slide' 'cards'
        "~Trouble with holiday snaps is, once somebody gets the old slide projector going, a boring time is on
        the cards for all.~"
    2 'rod' 'rods'
        "~You'll need to be wide awake and alert to work out what to do with them. Finding them is easy - just
        remember to keep an eye out for short, fat things.~"
    2 'tarot' 'sosostris'
        "~Clairvoyantes are expert at detecting stacked decks, so it's no good cheating.~" 
    2 'boat' 'hollow'
        "~Phlebas runs a very badly-advertised service, I'm afraid. I've got no time for him.~" 
    3 'mouse' 'robot' 'hole'
        "~Once the mouse is in the hole, forget it, it couldn't hear you even if you talked to the hole instead. But there is an alternative method.~" 
    'key'
        "~Your fault for letting the key get into the foundations - nothing can get in there to retrieve it.~" 
    2 'radio' 'wireless'
        "~Marvellous, marvellous contraption. Starts the moment you switch it on, and then nothing but solid gold classics all the way. You can really concentrate when that's playing.~" 
    2 'fireplace' 'chimney'
        "~What, the blocked-up old fireplace that doesn't lead anywhere?  You need to be carrying plenty of equipment to get down it - don't worry if it's dark at the bottom, after all there's nothing you can do about that.~" 
    2 'sandstone' 'recess'
        "~The sandstone recess is probably impassible. Better try only when you're wide awake.~" 
    2 'austin' 'cat'
        "~Austin's a perfect devil, isn't he?  She, I should say. Good for nothing except getting in the way, of course. Walks into walls just when you don't want, but at least Jemima calms her down.~" 
    3 'smoke' 'detector' 'alarm'
        "~Smoke detectors like yours are specially rigged not to be triggered by accidental shafts of light.~" 
    'ship'
        "~Of course the ship is far too large and ungainly ever to fit in the bottle.~" 
    'flag'
        "~Cold comfort.~" 
    2 'alison' 'bed'
        "~A good blanket might be a plain, colourless sort of covering with no emblem to it.~" 
    'revolving'
        "~The thing about the revolving door is, you can only revolve it when you're already standing inside.~" 
    2 'cabinet' 'glass'
        "~Unbreakable glass.~" 'matchbook'
        "~A vital clue, and useful in its own right.~" 
    'curses'
        "~The greatest computer game of all time. I'm proud to be a part of it.~" 
    'tera'
        "The demon blushes. ~Wasn't my fault!  My partner made a mess of it.~"
    "";       

Array angel_topics --> 3 'heaven' 'yourself' 'herself' "~Heaven?  Oh this isn't your eternal salvation, it's mine, able to serve by answering all these delightful questions. No, Heaven itself is a marvellous place, and it has an excellent health club too. Look forward to seeing you there.~" 2 'hell' 'demon' "The angel acknowledges the question, but cannot bring herself to speak of Hell." 'rods' "~The rods definitely fit into all this somewhere.~" 'maze' "~If only you could have been there when the maze was being laid out!~" 'master' "~The master game is like a point at infinity to we mortals,~ the angel says wistfully." 3 'infinity' 'lemniscus' 'arc' "~All a matter of fitting the rods in somewhere.~" 3 'andromeda' 'princess' 'maiden' "~How she must long to let her hair down now, instead of being chained to that rock,~ the angel says sorrowfully. ~But in deference to her wishes, I cannot help you rescue her.~" 'mural' "~Amazing fortune you must have in finding it, I can tell.~" 'folly' "~Ah yes, we must all lament for human folly,~ says the angel, misunderstanding completely." 'zeus' "~Zeus wasn't a true god, of course,~ the angel predictably insists, ~just a manifestation of the laws of physics to an Ancient Greek, a kind of symbol for the way the universe fundamentally worked.~" 2 'bomb' 'lighthouse' "The angel maintains a tight-lipped silence." 'sacrifice' "~That's too dangerous a rod to actually use, of course, and the Church no longer approves of martyrs.~" 'luck' "~Is there really such a thing as luck, or free will?  It's a theological grey area,~ says the angel with fine casuistry." 'fire' "~Certainly a thorny problem, that.~" 5 'mascot' 'old' 'evans' 'moon' 'racing' "~Old Evans would never give you his mascot of his own free will.~" 2 'ugly' 'idol' "~One has to reflect on ugliness like that.~" 3 'gold' 'golden' 'watch' "~It's quite hypnotically fascinating, don't you think?~" 4 'squirrel' 'loose' 'board' 'summerhou' "~Squirrels are very fond of nuts, but you can't trust them an inch.~" 'spade' "~Just think what that would do to a decent lawn!  Oh, it makes me go cold all over.~" 2 'croquet' 'lawn' "~Whatever is a croquet lawn for, if not to play croquet?  Although, now I think about it, there is something else down there.~" 2 'anoppe' 'eppona' "~Ah yes, a code word understood by certain of Alexander the Great's slaves. But you'd have to be in the right place at the right time.~" 2 'tombstone' 'tomb' "~The mouth is the key, or rather the lock.~" 2 'sphinx' 'sphinxes' "~Do be careful. The sphinxes are only made of stone, but they could easily give you nightmares.~" 2 'couch' 'couches' "~Jolly comfortable, some of those funeral couches.~" 5 'skiff' 'yacht' 'boat' 'coffin' 'mummy' "~A one-way ride, but that's life.~" 2 'adamantine' 'knight' "~Once fired with life, the knight will only be yours to command when he has discharged his chivalric duty.~" 2 'hand' 'head' "~Some of those ancient knights were rather loose, but perhaps that's better than being screwed up.~" 2 'library' 'portico' "~The Great Library isn't for passing hooligans, you know,~ says the angel reprovingly. ~Besides, the guards understand their duty by the Cat God far too well to let you through.~" 3 'librarian' 'apollonius' 'callimachus' "~They're always at each other's throats. I shudder to think what might happen if a real fight broke out.~" 2 'alexandria' 'processio' "~Oh, find yourself a costume, join the party!~" 3 'quarterst' 'quarter' 'staff' "~The green wood may one day make a really good staff, but it could take ages. Better leave it with someone for safe keeping.~" 'high' "~Some choices are inevitable and permanent, at least in this world.~" 'oubliette' "~Do be careful of the trap, won't you!  You might want to take precautions with the opening.~" 3 'amber' 'gem' 'gemstone' "~Let me see.~" 2 'statues' 'gods' "~You appreciate, in my position I can't really comment on graven images of pagan gods. Better try some dictionary or other.~" 4 'oracle' 'priestess' 'delphic' 'altar' "~That woman will rabbit on, once you set her off. But only if you sacrifice something. Will you be joining us next Harvest Sunday, by the way?~" 2 'oil' 'trough' "~Ooh, it does make me shiver, thinking of the way they used to pour oil over themselves, anointing they called it.~" 2 'homer' 'poet' "~Shabby old gossip, Homer, always reminds me of Peter Falk. Anyway, just give him his answers. Shouldn't be too hard, with what you're wearing and carrying.~" 3 'summer' 'house' 'summerhou' "~Going in round the front just scares the birds away, I fear.~" 3 'contraption' 'panel' 'grid' "~Find out what you can about your ancestor who built it - his tomb isn't far away. Then reflect as best you can on the problem.~" 'ozone' "~Well, if you know what the odour is, that's the main thing.~" "";       

[ AngelDevilConsultTopic tbl w ndx i len flag ;
    ndx = 0;
    while( 1 == 1 ) {
        len = 1;
        flag = false;
        if( tbl-->ndx == -1 ) {
            rfalse;
        }
        if( tbl-->ndx < 20 && tbl-->ndx >= 0 ) {
            len = tbl-->ndx;
            ++ndx;
        }
        i = 0;
        while( i < len ) {
            if (w == tbl-->ndx) {
                flag = true;
            }
            ++i;
            ++ndx;
        }
        if( flag == true ) {
            return tbl-->ndx;
        }
        ++ndx;
    }
    rtrue;
];

[ DemonQuestion w topic ;
    topic = AngelDevilConsultTopic(demon_topics,w);
    if( topic ~= 0 ) {
        print (string) topic;
        new_line;
        rtrue;
    }
    if( AngelDevilConsultTopic(angel_topics,w) ~= 0 ) {
        "~That's a question you'd better put to my colleague in the Other Place,~ says the demon smoothly. 
         ~Try something else.~";
    }
    print "~That's not something I am empowered to answer,~ says the demon with a nervous glance behind him. 
           ~Try something else.~^";
    rfalse;
];

[ AngelQuestion w topic ;
    topic = AngelDevilConsultTopic(angel_topics,w);
    if( topic ~= 0 ) {
        print (string) topic;
        new_line;
        rtrue;
    }
    if( AngelDevilConsultTopic(demon_topics,w) ~= 0 ) {
        "~That's a question you'd better put to my colleague in the Other Place,~ says the angel smoothly. 
         ~Try something else.~^";
    }
    print "~That's not something I am empowered to answer,~ says the angel with a beatific smile upward. 
           ~Try something else.~^";
    rfalse;
];


Object Hellish_Place "Hellish Place"
  with  name 'desk',
        after [ cnt;
            Look:
                new_line; 
                if( demon has general ) {
                    "~Thought what you wanted to ask yet?~ says the demon.";
                }
                cnt = demon.number;
                if( cnt > 0 ) {
                    --cnt;
                    demon.number = cnt;
                    if( cnt > 0 ) {
                        give demon general;
                    }
                }
                if( cnt == 0 ) {
                    "~Begone, pipsqueak!  I'll be seeing you... much later...~ says the demon with a memorable smile.";
                }
                if( cnt == 1 ) { 
                    "~All right, toad, you have one, just one, chance left,~ says the demon with a memorable smile.";
                }
                if( cnt == 2 ) {
                    "~Now then, two chances remaining,~ says the demon with a memorable smile.";
                }
                "~Good afternoon,~ says the demon with a memorable smile. ~As a reward for your naked greed, may I 
                 welcome you to our little information service. We try to be as helpful as possible to explorers
                 who are lost, or can't think how to solve their problems, you know the kind of thing. So just
                 tell me what you want to know, or show me something to look at. You get the usual three hints.~";
        ],
        n_to Cellar_West,
        u_to Cellar_West,
        description "Hell, you always imagined, would contain fiery, sulphurous pits and a great many gentlemen with 
            forked tails. You were right about the last part. A demon is sitting behind a flame-proofed desk at
            the bottom of the stairs. There are some disconcerting screams from further away, but nothing to worry
            about. Not in this life, anyway.",
        cant_go [;
            trigger_quotation(37);
            "That direction is not for the living.";
        ],
        each_turn [;
            switch( random(10) ) {
                1: "^There is a horrid grinding noise from far away.";
                2: "^A flash of light sears your eyes, as the sulphurous flames grumble.";
                3: "^You can almost, but not quite, make out the noise of shouting.";
                4: "^Thunder and lightning can be heard from the fiery depths.";
            }
        ],
        section T_AFTERLIFE,
  has   light;

Object -> demon "demon"
  with  name 'devil' 'demon' 'demonic' 'gentleman',
        life [ i;
            Ask, Answer:
                if( HellishAsk() ~= 0 ) rtrue;
                if( DemonQuestion(special_word) ~= 0 ) give demon ~general;
                rtrue;
            Order: "One does not give orders to such a character.";
            Give, Show:
                if( HellishAsk() ~= 0 ) rtrue;
                print "The demon glances lazily at ", (the) noun, " and rouses himself to say: ~";
                give demon ~general;
                if( noun has edible ) {
                    remove noun;
                    print "Mmm... delicious!~  Sloth and gluttony... two deadly sins in one minute. His boss will be pleased. ~";
                }
                i = noun.devil_hint;
                if( i == -1 || i == nothing ) {
                    if( noun.angel_hint ~= "" )
                        "I know about that, but my counterpart doesn't.~";
                    "I know just what that's for.~";
                }
                print_ret (string) i, "~";
        ],
        description "If you were to cast a film of your adventures so far, you would hire Donald Sutherland to 
            play this gentleman.",
        number 4,
  has   animate scenery;

Object Heavenly_Place "Heavenly Place"
  with  name 'furniture' 'cloud',
        before [;
            Strike: "The clouds are too fluffy."; 
        ],
        after [ i;
            Look:
                new_line;
                if( angel has general ) {
                    "~Take all the time you need,~ says the angel kindly.";
                }
                i = angel.number;
                if( i > 0 ) { 
                    --i;
                    angel.number = i;
                    if( i > 0 ) { 
                        give angel general;
                    }
                }
                if( i == 0 ) { 
                    "~I'm afraid I can be of no further use to you!  See you in another life,~ says the angel with a memorable smile.";
                }
                if( i == 1 ) { 
                    "~You have one remaining question,~ says the angel with a memorable smile."; 
                }
                if( i == 2 ) { 
                    "~A whole two questions left,~ says the angel with a memorable smile.";
                }
                "~Good day,~ says the angel with a memorable smile. ~As a reward for your act of faith, may I welcome 
                 you to our little information service. We try to be as helpful as possible to explorers who are lost,
                 or can't think how to solve their problems. Is there anything, pray, which you would like a hint
                 about, or which you'd like to show me?  You are permitted three hints in all, in accordance with
                 standard practice.~";
        ],
        s_to Up_the_Beanstalk,
        description "Heaven, you always imagined, would be a world of marble pillars, fluffy clouds, harps and 
            angelic ladies with serene expressions. You were right about the last part. An angel is sitting here
            behind some new office furniture, next to a blue Tourist Information sign. A bridge of cloud extends
            south across the sky to the beanstalk. There is some choral singing going on somewhere, but not loud
            enough for you to hear properly. Not in this life, anyway.",
        cant_go [;
            trigger_quotation(27);
            "That direction is not for the living.";
        ],
        each_turn [;
            switch( random(10) ) {
                1: "^Angels with harps float by, gossiping about you in a kindly way.";
                2: "^Babies float by, just counting their toes.";
                3: "^Burnished-gold sunlight strokes the tops of the clouds here.";
                4: "^The choir invisible almost, for a moment, becomes audible.";
            }
        ],
        section T_AFTERLIFE,
  has   light;

Object -> angel "angel"
  with  name 'angel' 'lady' 'angelic',
        life [ i;
            Answer, Ask:
                if( AngelHints() ~= 0 ) rtrue;
                if( AngelQuestion(special_word) ~= 0 )
                    give demon ~general;   !! Bug, I think. Should be angel
                rtrue;
            Order: "One does not give orders to such a character.";
            Give, Show:
                if( AngelHints() ~= 0 ) rtrue;
                print "The angel looks keenly at ", (the) noun, " and rouses himself to say: ~";
                give angel ~general;
                i = noun.angel_hint;
                if( i == -1 || i == nothing ) {
                    if( noun.devil_hint ~= "" )
                        "I suppose you could ask, um, that tailed gentleman.~";
                    "I'm afraid I can't help you there.~";
                }
                print_ret (string) i, "~";
        ],
        description "If you were to cast a film of your adventures so far, you would hire Dame Judi Dench to play this lady.",
        number 4,
  has   animate scenery;

[ HellishAsk ;
    if( demon hasnt general ) {
        if( demon.number == 0 ) {
            "~No hints left, sucker.~";
        }
        "~Under Regulation 3, paragraph 15 (i) clause (f), only one hint per visit to the infernal domain. You'll have to go away again and come back if you want another. So sorry.~";
    }
    rfalse;
];

[ AngelHints ;  
    if( angel hasnt general ) {
        if( angel.number == 0 ) {
            "~No hints left, alas.~";
        }   
        "~Under Regulation 3, paragraph 15 (i) clause (f), only one hint per visit to the sacred domain. You'll have to go away again and come back if you want another. So sorry.~";
    }
    rfalse;
];       

